title 'Test FILE LABEL, DOCUMENT, DROP DOCUMENTS'.

/* Set up a dummy active file in memory.
data list /x 1 y 2.
begin data.
16
27
38
49
50
end data.

/* Add value labels for some further testing of value labels.
value labels x y 1 'first label' 2 'second label' 3 'third label'.
add value labels x 1 'first label mark two'.

/* Add a file label and a few documents.
file label This is a test file label.
document First line of a document
This is the second very long line of a document in an attempt to overflow the input buffer with a really long line
Note that the last line should end with a period: .

/* Display the documents.
display documents.
display file label.	/* undocumented feature of PSPP

/* Save the active file then get it and display the documents again.
save 'foo.save'.
get 'foo.save'.
display documents.
display file label.	/* undocumented feature of PSPP

/* There is an interesting interaction that occurs if the `execute'
/* command below.  What happens is that an error message is output
/* at the next `save' command that `foo.save' is already open for
/* input.  This is because the `get' hasn't been executed yet and
/* therefore PSPP would be reading from and writing to the same
/* file at once, which is obviously a Bad Thing.  But `execute'
/* here clears up that potential problem.
execute.

/* Add another (shorter) document and try again.
document There should be another document now.
display documents.

/* Save and get.
save 'foo.save'.
get 'foo.save'.
display documents.
display file label.	/* undocumented feature of PSPP

/* Done.

